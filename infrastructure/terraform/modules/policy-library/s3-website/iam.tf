locals {
  s3_bucket_namespace_arn = "arn:aws:s3:::${var.prefix}-*"
  s3_object_namespace_arn = "arn:aws:s3:::${var.prefix}-*/*"
}

data "aws_iam_policy_document" "this" {
  # Bucket creation cannot be scoped because the bucket does not exist yet.
  statement {
    sid       = "S3BucketCreate"
    effect    = "Allow"
    actions   = ["s3:CreateBucket"]
    resources = ["*"]
  }

  statement {
    sid    = "S3BucketManagement"
    effect = "Allow"
    actions = [
      "s3:DeleteBucket",
      "s3:ListBucket",
      "s3:PutBucketVersioning",
      "s3:GetBucketPublicAccessBlock",
      "s3:PutBucketPublicAccessBlock",
      "s3:PutBucketPolicy",
      "s3:DeleteBucketPolicy",
      "s3:PutBucketTagging",
      "s3:PutBucketAcl",
      "s3:PutBucketCORS",
      "s3:PutBucketWebsite",
      "s3:Get*"
    ]
    resources = [local.s3_bucket_namespace_arn]
  }

  statement {
    sid    = "S3ObjectManagement"
    effect = "Allow"
    actions = [
      "s3:GetObject",
      "s3:GetObjectTagging",
      "s3:PutObject",
      "s3:PutObjectTagging",
      "s3:DeleteObject"
    ]
    resources = [local.s3_object_namespace_arn]
  }

  statement {
    sid    = "KmsForS3"
    effect = "Allow"
    actions = [
      "kms:Decrypt",
      "kms:GenerateDataKey",
      "kms:DescribeKey"
    ]
    resources = ["*"]
    condition {
      test     = "StringEquals"
      variable = "kms:ViaService"
      values   = ["s3.us-east-1.amazonaws.com"]
    }
  }
}
